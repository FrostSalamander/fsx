
// GLOBALS
float JIMMY_SPAWNED = 0;
float COATES_SPAWNED = 0;

// Entities

// OBJECTIVES
float OBJ_KILL_COATES = 1;
float OBJ_SAFE_HOUSE = 2;
float OBJ_FREE_COOK = 3;
float OBJ_LEAVE_DAGGER = 4;

// Apartments multistate position entities
string POS_APT_FLOOR_0 = "pos_apt_floor0";
string POS_APT_FLOOR_1 = "pos_apt_floor1";

// Jimmy Cook objective state target
string TARGET_STATE_COOK = "target_set_cook_objective_state";

// Prison cell doors
string PRISON_DOOR_LW = "VaultDoorLW";
string PRISON_DOOR_UW = "VaultDoorUW";

// Pop-up message entities
string DISABLE_PRISON_POPUP = "target_disable_prison_popup";
string TRIGGER_NEWSPAPER_POPUP = "trigger_newspaper_popup";

// Triggers
string TRIGGER_ATKINSON_OBJECTIVE = "trigger_relay_atkinson_objective";

boolean isBindChildrenOpen(entity mover)
{
    float bindChildren = mover.numBindChildren();
    float i;

    for (i=0; i < bindChildren; i++)
    {
        entity bindChild = mover.getBindChild(i);
        //sys.println(bindChild.getName());
        if (bindChild.IsOpen())
        {
            //sys.println("bind child" + bindChild.getName() + " is open");Â¦
            return true;
        }
    }
    return false;
}

void controlLiftDoors(entity target, entity button, entity callscriptfunction)
{
    // The button that calls this is targeting the lift with 'target' spawnarg (i.e. not 'target0', 'target1', etc.)
    entity lift = button.getEntityKey("target");

    // Get the multistate position entity this button corresponds to
    // custom spawnarg: didn't want to use 'target' or 'position' because they activate unwanted functionality on some buttons
    entity multiStatePosition = button.getEntityKey("position_entity_name"); 

    // ensure the lift is idle and it's on the appropriate floor
    if ( !lift.isMoving() && (lift.getOrigin() == multiStatePosition.getOrigin()) )
    {
        if (target.IsOpen())
        {
            target.Close();
        }
        else
        {
            target.Open();
        }
    }
    else
    {
        sys.println("lift is on wrong floor or is moving");
    }
}

void spawnAI(string origin, string target, string class)
{
    sys.setSpawnArg("origin", origin);

    // target their path_corner
    sys.setSpawnArg("target0", target);

    entity spawnedAI = sys.spawn(class);
}

entity spawnEntity(string origin, string rotation, string class)
{
    sys.setSpawnArg("origin", origin);
    sys.setSpawnArg("rotation", rotation);
    
    return sys.spawn(class);
}

void spawnJimmy(string aiOrigin, string target, string letterOrigin, string cellDoor)
{
    if (!JIMMY_SPAWNED)
    {
        JIMMY_SPAWNED = 1;

        sys.println("spawning Jimmy..."); // TODO: DEBUG_REMOVE
        spawnAI(aiOrigin, target, "fsx:ai_jimmy_cook");

        // spawn the letter
        spawnEntity(letterOrigin, "-0.341166 -0.940003 0 0.940003 -0.341166 0 0 0 1", "fsx:jimmy_letter");

        // target the objective state entity with the cell door
        sys.getEntity(cellDoor).addTarget(sys.getEntity(TARGET_STATE_COOK));

    }
}

// Spawn Jimmy in an upper west cell
void spawnJimmyUpperWest()
{
    spawnJimmy("1904.16 3380.72 378.885", "path_corner_85", "1869.96 3272.06 404.007", PRISON_DOOR_UW);
}

// Spawn Jimmy in a lower west cell
void spawnJimmyLowerWest()
{
    spawnJimmy("1896.16 3644.72 194.932", "path_corner_86", "1876.96 3508.05 220.007", PRISON_DOOR_LW);
}

void spawnCoates(string origin, string target)
{
    if (!COATES_SPAWNED)
    {
        sys.println("spawning Coates..."); // TODO: DEBUG_REMOVE
        spawnAI(origin, target, "fsx:ai_vinton_coates");
        COATES_SPAWNED = 1;
    }
}

// Spawn Coates in the church
// Applies to the following zones:
//   - location_hotel_lobby

void spawnCoatesChurch(entity oldZone)
{
    spawnCoates("3408 4728 8.0669", "path_corner_111");
}

// Spawn Coates in the Hotel
// Applies to the following zones:
//   - location_mansion_lounge
//   - location_mansion_pool_room

void spawnCoatesHotel(entity oldZone)
{
    spawnCoates("1199.49 -42.5907 32.0669", "path_corner_18");
}

// Spawn Coates in the Mansion
// Applies to the following zones:
//   - location_church_belfry_quarters

void spawnCoatesMansion(entity oldZone)
{
    spawnCoates("400 4352 384.067", "path_wait_60");
}

boolean isObjectiveComplete(float objNum)
{
    return ($player1.getObjectiveState(objNum) == OBJ_COMPLETE);
}

boolean isObjectiveVisible(float objNum)
{
    return ($player1.getObjectiveVisible(2) == 1);
}

void triggerSafeHouseObjective()
{
    if (!isObjectiveVisible(OBJ_SAFE_HOUSE))
    {
        if (isObjectiveComplete(OBJ_KILL_COATES))
        {
            // The kill objective may have just displayed, so wait a bit until displaying the next one
            sys.wait(3);
            $player1.setObjectiveText(OBJ_SAFE_HOUSE, "Well done. Now check in at Lennard's safe house");
        }
        else
        {
            $player1.setObjectiveText(OBJ_SAFE_HOUSE, "Your mission has been exposed! Check in at Lennard's safe house for direction.");
        }

        // set the safe house objective visible
        $player1.setObjectiveVisible(OBJ_SAFE_HOUSE, 1);

        // Spawn prison cell key at Lennard's
        spawnEntity("335.972 2295.93 223.278", "0.592432 0.80562 0 -0.80562 0.592432 0 0 0 1", "fsx:prison_cell_key");

        // Spawn the letter
        spawnEntity("320 2294 222.881", "1 0 0 0 1 0 0 0 1", "fsx:safe_house_letter");

        // Spawn the newspaper. We prefer if the player finds this in the Review Office, but
        // in case they never find it they can read it here after Coates is killed.
        spawnEntity("363.295 2236.69 21.3811", "0.759739 0.650229 0 -0.650229 0.759739 0 0 0 1", "fsx:newspaper");
    }
    else
    {
        // If you've killed Coates but haven't yet been to the safe house, display a pop up if you read the newspaper.
        // Just gives 'some reaction' to seeing the news.

        // This scenario can happen if you start in Old Town and then eventually kill Coates in the hotel
        // before you get to The Review
        if (isObjectiveComplete(OBJ_KILL_COATES) && !isObjectiveComplete(OBJ_SAFE_HOUSE))
        {
            sys.trigger(sys.getEntity(TRIGGER_NEWSPAPER_POPUP));
        } 
    }
}

void completeSafeHouseObjective()
{
    // complete the 'get to the safe house' objective
    $player1.setObjectiveState(OBJ_SAFE_HOUSE, OBJ_COMPLETE);

    // disable the prison popup message
    sys.trigger(sys.getEntity(DISABLE_PRISON_POPUP));

    sys.wait(3);

    // make the 'free Cook' objectives visible
    $player1.setObjectiveVisible(OBJ_FREE_COOK, 1);
    $player1.setObjectiveVisible(OBJ_LEAVE_DAGGER, 1);
}

void frobMask(entity ent)
{
    // trigger relay that does various things
    sys.trigger($trigger_relay_demon_off);

    // respawn gregory ragdoll so it falls to the bed
    sys.spawn("fsx:ragdoll_gregory");

    // original frob action functionality
    frob_item(ent);

    // update location entity with new ambient settings
    $location_abbey_loft.setKey("ambient", "snd_abbey");
    $location_abbey_loft.setKey("ambient_light", "0.04 0.04 0.04");
}

void frobAtkinson()
{
    sys.trigger(sys.getEntity(TRIGGER_ATKINSON_OBJECTIVE));
}

// main - keep at bottom
void main()
{
    sys.waitFrame();
}